plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url "https://frcmaven.wpi.edu/artifactory/release/"
    }
    maven {
        url "https://maven.revrobotics.com/"
    }
}

def WPIversion = '2022.1.1'
def REVversion = '2022.0.0'

dependencies {
    implementation "edu.wpi.first.wpilibj:wpilibj-java:$WPIversion"
    implementation "edu.wpi.first.wpiutil:wpiutil-java:$WPIversion"
    implementation "edu.wpi.first.hal:hal-java:$WPIversion"
    implementation "com.revrobotics.frc:REVLib-java:$REVversion"
}

test {
    useJUnitPlatform()
}

// name = 'SparkMaxSim-java'
group = System.getenv("GROUP") ?: 'net.thefletcher'
version = System.getenv("VERSION") ?: REVversion

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withSourcesJar()
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}
